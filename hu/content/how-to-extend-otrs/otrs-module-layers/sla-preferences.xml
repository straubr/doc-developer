<?xml version='1.0' encoding='UTF-8'?>
<!DOCTYPE section PUBLIC "-//OASIS//DTD DocBook XML V4.4//EN" "http://www.oasis-open.org/docbook/xml/4.4/docbookx.dtd">


<section id="sla-preferences-module">
    <title>SLA beállítások modul</title>
    <para>
        Létezik egy DB SLA beállítások modul, amely az OTRS keretrendszerrel
érkezik. Lehetőség van saját SLA beállítási modulok fejlesztésére is. Az SLA
beállítási modulok a <filename>Kernel/System/SLA/*.pm</filename> alatt
találhatók. Ezek beállításáról további információkért nézze meg az
adminisztrátori kézikönyvet. A következőkben egy SLA beállítások modul
példája található. Mentse el a
<filename>Kernel/System/SLA/PreferencesCustom.pm</filename>
helyre. Mindössze három függvényre van szüksége: <code>new()</code>,
<code>SLAPreferencesSet()</code> és
<code>SLAPreferencesGet()</code>. Győződjön meg arról, hogy a függvény 1-et
adjon vissza.
    </para>
    <section id="sla-preferences-module-code">
        <title>Kódpélda</title>
        <para>
            A felületosztály neve <literal>Kernel::System::SLA</literal>. A példa SLA
beállítások hívhatók
<literal>Kernel::System::SLA::PreferencesCustom</literal> néven. Lent
találhat egy példát.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
# --
# Kernel/System/SLA/PreferencesCustom.pm - néhány felhasználói függvény
# Copyright (C) 2001-2016 OTRS AG, http://otrs.com/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::SLA::PreferencesCustom;

use strict;
use warnings;

use vars qw(@ISA);

sub new {
    my ( $Type, %Param ) = @_;

    # új kivonat lefoglalása az objektumhoz
    my $Self = {};
    bless( $Self, $Type );

    # a szükséges objektumok ellenőrzése
    for (qw(DBObject ConfigObject LogObject)) {
        $Self->{$_} = $Param{$_} || die "Nincs $_!";
    }

    # beállítások tábla adatai
    $Self->{PreferencesTable}      = 'sla_preferences';
    $Self->{PreferencesTableKey}   = 'preferences_key';
    $Self->{PreferencesTableValue} = 'preferences_value';
    $Self->{PreferencesTableSLAID} = 'sla_id';

    return $Self;
}

sub SLAPreferencesSet {
    my ( $Self, %Param ) = @_;

    # a szükséges dolgok ellenőrzése
    for (qw(SLAID Key Value)) {
        if ( !defined( $Param{$_} ) ) {
            $Self->{LogObject}->Log( Priority => 'error', Message => "Szükséges: $_!" );
            return;
        }
    }

    # régi adatok törlése
    return if !$Self->{DBObject}->Do(
        SQL => "DELETE FROM $Self->{PreferencesTable} WHERE "
            . "$Self->{PreferencesTableSLAID} = ? AND $Self->{PreferencesTableKey} = ?",
        Bind => [ \$Param{SLAID}, \$Param{Key} ],
    );

$Self->{PreferencesTableValue} .= 'PreferencesCustom';

    # új adatok beszúrása
    return $Self->{DBObject}->Do(
        SQL => "INSERT INTO $Self->{PreferencesTable} ($Self->{PreferencesTableSLAID}, "
            . " $Self->{PreferencesTableKey}, $Self->{PreferencesTableValue}) "
            . " VALUES (?, ?, ?)",
        Bind => [ \$Param{SLAID}, \$Param{Key}, \$Param{Value} ],
    );
}

sub SLAPreferencesGet {
    my ( $Self, %Param ) = @_;

    # a szükséges dolgok ellenőrzése
    for (qw(SLAID)) {
        if ( !$Param{$_} ) {
            $Self->{LogObject}->Log( Priority => 'error', Message => "Szükséges: $_!" );
            return;
        }
    }

    # annak ellenőrzése, hogy az SLA beállítások elérhetők-e
    if ( !$Self->{ConfigObject}->Get('SLAPreferences') ) {
        return;
    }

    # beállítások lekérése
    return if !$Self->{DBObject}->Prepare(
        SQL => "SELECT $Self->{PreferencesTableKey}, $Self->{PreferencesTableValue} "
            . " FROM $Self->{PreferencesTable} WHERE $Self->{PreferencesTableSLAID} = ?",
        Bind => [ \$Param{SLAID} ],
    );
    my %Data;
    while ( my @Row = $Self->{DBObject}->FetchrowArray() ) {
        $Data{ $Row[0] } = $Row[1];
    }

    # adatok visszaadása
    return %Data;
}

1;

        ]]></programlisting>
        </para>
    </section>
    <section id="sla-preferences-module-configuration">
        <title>Beállítási példa</title>
        <para>
            Szükség van az egyéni SLA beállítások modul bekapcsolására. Ezt a lenti XML
beállítás használatával lehet megtenni.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
<ConfigItem Name="SLA::PreferencesModule" Required="1" Valid="1">
    <Description Translatable="1">Default SLA preferences module.</Description>
    <Group>Ticket</Group>
    <SubGroup>Frontend::SLA::Preferences</SubGroup>
    <Setting>
        <String Regex="">Kernel::System::SLA::PreferencesCustom</String>
    </Setting>
</ConfigItem>
        ]]></programlisting>
        </para>
    </section>
    <section id="sla-preferences-module-use_cases">
        <title>Használati eset példa</title>
        <para>
            Hasznos beállítások megvalósítás lehet további értékek tárolása az SLA-khoz.
        </para>
    </section>
    <section id="sla-preferences-module-releases">
        <title>Kiadási elérhetőség</title>
        <para>
        <informaltable>
            <tgroup cols="2">
                <thead>
                    <row>
                        <entry>Név</entry>
                        <entry>Kiadás</entry>
                    </row>
                </thead>
            <tbody>
                    <row>
                        <entry>PreferencesDB</entry>
                        <entry>2.4</entry>
                    </row>
            </tbody>
            </tgroup>
        </informaltable>
        </para>
    </section>
</section>
