<?xml version='1.0' encoding='UTF-8'?>
<!DOCTYPE section PUBLIC "-//OASIS//DTD DocBook XML V4.4//EN" "http://www.oasis-open.org/docbook/xml/4.4/docbookx.dtd">


<section id="service-preferences-module">
    <title>Szolgáltatás beállítások modul</title>
    <para>
        Létezik egy DB szolgáltatás beállítások modul, amely az OTRS
keretrendszerrel érkezik. Lehetőség van saját szolgáltatás beállítási
modulok fejlesztésére is. A szolgáltatás beállítási modulok a
<filename>Kernel/System/Service/*.pm</filename> alatt találhatók. Ezek
beállításáról további információkért nézze meg az adminisztrátori
kézikönyvet. A következőkben egy szolgáltatás beállítások modul példája
található. Mentse el a
<filename>Kernel/System/Service/PreferencesCustom.pm</filename>
helyre. Mindössze három függvényre van szüksége: <code>new()</code>,
<code>ServicePreferencesSet()</code> és
<code>ServicePreferencesGet()</code>. Adjon vissza 1-et, és ezután a
szinkronizáció rendben van.
    </para>
    <section id="service-preferences-module-code">
        <title>Kódpélda</title>
        <para>
            A felületosztály neve <literal>Kernel::System::Service</literal>. A példa
szolgáltatás beállítások hívhatók
<literal>Kernel::System::Service::PreferencesCustom</literal> néven. Lent
találhat egy példát.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
# --
# Kernel/System/Service/PreferencesCustom - néhány felhasználói függvény
# Copyright (C) 2001-2016 OTRS AG, http://otrs.com/
# --
# Id: PreferencesCustom.pm,v 1.2 2009/02/16 11:47:34 tr Exp $
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::Service::PreferencesCustom;

use strict;
use warnings;

use vars qw(@ISA $VERSION);

sub new {
    my ( $Type, %Param ) = @_;

    # új kivonat lefoglalása az objektumhoz
    my $Self = {};
    bless( $Self, $Type );

    # a szükséges objektumok ellenőrzése
    for (qw(DBObject ConfigObject LogObject)) {
        $Self->{$_} = $Param{$_} || die "Nincs $_!";
    }

    # beállítások tábla adatai
    $Self->{PreferencesTable}          = 'service_preferences';
    $Self->{PreferencesTableKey}       = 'preferences_key';
    $Self->{PreferencesTableValue}     = 'preferences_value';
    $Self->{PreferencesTableServiceID} = 'service_id';

    return $Self;
}

sub ServicePreferencesSet {
    my ( $Self, %Param ) = @_;

    # a szükséges dolgok ellenőrzése
    for (qw(ServiceID Key Value)) {
        if ( !defined( $Param{$_} ) ) {
            $Self->{LogObject}->Log( Priority => 'error', Message => "Szükséges: $_!" );
            return;
        }
    }

    # régi adatok törlése
    return if !$Self->{DBObject}->Do(
        SQL => "DELETE FROM $Self->{PreferencesTable} WHERE "
            . "$Self->{PreferencesTableServiceID} = ? AND $Self->{PreferencesTableKey} = ?",
        Bind => [ \$Param{ServiceID}, \$Param{Key} ],
    );

$Self->{PreferencesTableValue} .= 'PreferencesCustom';

    # új adatok beszúrása
    return $Self->{DBObject}->Do(
        SQL => "INSERT INTO $Self->{PreferencesTable} ($Self->{PreferencesTableServiceID}, "
            . " $Self->{PreferencesTableKey}, $Self->{PreferencesTableValue}) "
            . " VALUES (?, ?, ?)",
        Bind => [ \$Param{ServiceID}, \$Param{Key}, \$Param{Value} ],
    );
}

sub ServicePreferencesGet {
    my ( $Self, %Param ) = @_;

    # a szükséges dolgok ellenőrzése
    for (qw(ServiceID)) {
        if ( !$Param{$_} ) {
            $Self->{LogObject}->Log( Priority => 'error', Message => "Szükséges: $_!" );
            return;
        }
    }

    # annak ellenőrzése, hogy a szolgáltatás beállítások elérhetők-e
    if ( !$Self->{ConfigObject}->Get('ServicePreferences') ) {
        return;
    }

    # beállítások lekérése
    return if !$Self->{DBObject}->Prepare(
        SQL => "SELECT $Self->{PreferencesTableKey}, $Self->{PreferencesTableValue} "
            . " FROM $Self->{PreferencesTable} WHERE $Self->{PreferencesTableServiceID} = ?",
        Bind => [ \$Param{ServiceID} ],
    );
    my %Data;
    while ( my @Row = $Self->{DBObject}->FetchrowArray() ) {
        $Data{ $Row[0] } = $Row[1];
    }

    # adatok visszaadása
    return %Data;
}

1;

        ]]></programlisting>
        </para>
    </section>
    <section id="service-preferences-module-configuration">
        <title>Beállítási példa</title>
        <para>
            Szükség van az egyéni szolgáltatás beállítások modul bekapcsolására. Ezt a
lenti XML beállítás használatával lehet megtenni.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
<ConfigItem Name="Service::PreferencesModule" Required="1" Valid="1">
    <Description Lang="en">Default service preferences module.</Description>
    <Description Lang="hu">Alapértelmezett szolgáltatás beállítások modul.</Description>
    <Group>Ticket</Group>
    <SubGroup>Frontend::Service::Preferences</SubGroup>
    <Setting>
        <String Regex="">Kernel::System::Service::PreferencesCustom</String>
    </Setting>
</ConfigItem>
        ]]></programlisting>
        </para>
    </section>
    <section id="service-preferences-module-use_cases">
        <title>Használati eset példa</title>
        <para>
            Hasznos beállítások megvalósítás lehet egy SOAP vagy egy RADIUS
háttérprogram.
        </para>
    </section>
    <section id="service-preferences-module-releases">
        <title>Kiadási elérhetőség</title>
        <para>
        <informaltable>
            <tgroup cols="2">
                <thead>
                    <row>
                        <entry>Név</entry>
                        <entry>Kiadás</entry>
                    </row>
                </thead>
            <tbody>
                    <row>
                        <entry>PreferencesDB</entry>
                        <entry>2.4</entry>
                    </row>
            </tbody>
            </tgroup>
        </informaltable>
        </para>
    </section>
</section>
