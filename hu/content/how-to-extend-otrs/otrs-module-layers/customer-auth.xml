<?xml version='1.0' encoding='UTF-8'?>
<!DOCTYPE section PUBLIC "-//OASIS//DTD DocBook XML V4.4//EN" "http://www.oasis-open.org/docbook/xml/4.4/docbookx.dtd">


<section id="customer-auth-module">
    <title>Ügyfél hitelesítő modul</title>
    <para>
        Számos ügyfél hitelesítő modul létezik (DB, LDAP és HTTPBasicAuth), amelyek
az OTRS keretrendszerrel érkeznek. Lehetőség van saját hitelesítő modulok
fejlesztésére is. Az ügyfél hitelesítő modulok a
<filename>Kernel/System/CustomerAuth/*.pm</filename> alatt találhatók. Ezek
beállításáról további információkért nézze meg az adminisztrátori
kézikönyvet. Ezt követően egy egyszerű ügyfél hitelesítő modul példája
található. Mentse el a
<filename>Kernel/System/CustomerAuth/Simple.pm</filename> helyre. Mindössze
három függvényre van szüksége: <code>new()</code>, <code>GetOption()</code>
és <code>Auth()</code>. Adja vissza az uid-t, és ezután a hitelesítés
rendben van.
    </para>
    <section id="customer-auth-module-code">
        <title>Kódpélda</title>
        <para>
            A felületosztály neve <literal>Kernel::System::CustomerAuth</literal>. A
példa ügyfél hitelesítés hívható
<literal>Kernel::System::CustomerAuth::CustomAuth</literal> néven. Lent
találhat egy példát.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
# --
# Kernel/System/CustomerAuth/CustomAuth.pm - az egyéni hitelesítést biztosítja
# Martin Edenhofer Kernel::System::Auth::DB osztálya alapján
# Copyright (C) 2001-2016 OTRS AG, http://otrs.com/
# --
# Id: CustomAuth.pm,v 1.11 2009/09/22 15:16:05 mb Exp $
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::CustomerAuth::CustomAuth;

use strict;
use warnings;

use Authen::CustomAuth;

sub new {
    my ( $Type, %Param ) = @_;

    # új kivonat lefoglalása az objektumhoz
    my $Self = {};
    bless( $Self, $Type );

    # a szükséges objektumok ellenőrzése
    for (qw(LogObject ConfigObject DBObject)) {
        $Self->{$_} = $Param{$_} || die "Nincs $_!";
    }

    # Hibakeresés: 0 = ki, 1 = be
    $Self->{Debug} = 0;

    # beállítás lekérése
    $Self->{Die}
        = $Self->{ConfigObject}->Get( 'Customer::AuthModule::CustomAuth::Die' . $Param{Count} );

    # felhasználótábla lekérése
    $Self->{CustomAuthHost}
        = $Self->{ConfigObject}->Get( 'Customer::AuthModule::CustomAuth::Host' . $Param{Count} )
        || die "Customer::AuthModule::CustomAuth::Host$Param{Count} szükséges ebben: Kernel/Config.pm";
    $Self->{CustomAuthSecret}
        = $Self->{ConfigObject}->Get( 'Customer::AuthModule::CustomAuth::Password' . $Param{Count} )
        || die "Customer::AuthModule::CustomAuth::Password$Param{Count} szükséges ebben: Kernel/Config.pm";

    return $Self;
}

sub GetOption {
    my ( $Self, %Param ) = @_;

    # a szükséges dolgok ellenőrzése
    if ( !$Param{What} ) {
        $Self->{LogObject}->Log( Priority => 'error', Message => "Mi szükséges!" );
        return;
    }

    # modul beállítások
    my %Option = ( PreAuth => 0, );

    # beállítás visszaadása
    return $Option{ $Param{What} };
}

sub Auth {
    my ( $Self, %Param ) = @_;

    # a szükséges dolgok ellenőrzése
    if ( !$Param{User} ) {
        $Self->{LogObject}->Log( Priority => 'error', Message => "Felhasználó szükséges!" );
        return;
    }

    # paraméterek lekérése
    my $User       = $Param{User}      || '';
    my $Pw         = $Param{Pw}        || '';
    my $RemoteAddr = $ENV{REMOTE_ADDR} || 'A REMOTE_ADDR környezetet nem sikerült lekérni!';
    my $UserID     = '';
    my $GetPw      = '';

    # csak hibakeresés esetén!
    if ( $Self->{Debug} > 0 ) {
        $Self->{LogObject}->Log(
            Priority => 'notice',
            Message  => "Felhasználó: „$User” megpróbált a következő jelszóval hitelesíteni: „$Pw” ($RemoteAddr)",
        );
    }

    # csak egy megjegyzés
    if ( !$User ) {
        $Self->{LogObject}->Log(
            Priority => 'notice',
            Message  => "Nincs felhasználó megadva!!! (REMOTE_ADDR: $RemoteAddr)",
        );
        return;
    }

    # csak egy megjegyzés
    if ( !$Pw ) {
        $Self->{LogObject}->Log(
            Priority => 'notice',
            Message  => "Felhasználó: $User hitelesítés jelszó nélkül!!! (REMOTE_ADDR: $RemoteAddr)",
        );
        return;
    }

    # Egyéni objektum létrehozása
    my $CustomAuth = Authen::CustomAuth->new(
        Host   => $Self->{CustomAuthHost},
        Secret => $Self->{CustomAuthSecret},
    );
    if ( !$CustomAuth ) {
        if ( $Self->{Die} ) {
            die "Nem lehet kapcsolódni ehhez: $Self->{CustomAuthHost}: $@";
        }
        else {
            $Self->{LogObject}->Log(
                Priority => 'error',
                Message  => "Nem lehet kapcsolódni ehhez: $Self->{CustomAuthHost}: $@",
            );
            return;
        }
    }
    my $AuthResult = $CustomAuth->check_pwd( $User, $Pw );

    # bejelentkezési jegyzet
    if ( defined($AuthResult) && $AuthResult == 1 ) {
        $Self->{LogObject}->Log(
            Priority => 'notice',
            Message  => "Felhasználó: $User hitelesítése OK (REMOTE_ADDR: $RemoteAddr).",
        );
        return $User;
    }

    # csak egy jegyzet
    else {
        $Self->{LogObject}->Log(
            Priority => 'notice',
            Message  => "Felhasználó: $User hitelesítése rossz jelszóval!!! (REMOTE_ADDR: $RemoteAddr)"
        );
        return;
    }
}

1;

        ]]></programlisting>
        </para>
    </section>
    <section id="customer-auth-module-configuration">
        <title>Beállítási példa</title>
        <para>
            Szükség van az egyéni ügyfél hitelesítő modul bekapcsolására. Ezt a lenti
XML beállítás használatával lehet megtenni.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
<ConfigItem Name="AuthModule" Required="1" Valid="1">
    <Description Lang="en">Module to authenticate customers.</Description>
    <Description Lang="hu">Egy modul az ügyfelek hitelesítéséhez.</Description>
    <Group>Framework</Group>
    <SubGroup>Frontend::CustomerAuthAuth</SubGroup>
    <Setting>
        <Option Location="Kernel/System/CustomerAuth/*.pm" SelectedID="Kernel::System::CustomerAuth::CustomAuth"></Option>
    </Setting>
</ConfigItem>
        ]]></programlisting>
        </para>
    </section>
    <section id="customer-auth-module-use_cases">
        <title>Használati eset példa</title>
        <para>
            Hasznos hitelesítés megvalósítás lehet egy SOAP háttérprogram.
        </para>
    </section>
    <section id="customer-auth-module-releases">
        <title>Kiadási elérhetőség</title>
        <para>
        <informaltable>
            <tgroup cols="2">
                <thead>
                    <row>
                        <entry>Név</entry>
                        <entry>Kiadás</entry>
                    </row>
                </thead>
                <tbody>
                    <row>
                        <entry>DB</entry>
                        <entry>1.0</entry>
                    </row>
                    <row>
                        <entry>HTTPBasicAuth</entry>
                        <entry>1.2</entry>
                    </row>
                    <row>
                        <entry>LDAP</entry>
                        <entry>1.0</entry>
                    </row>
                    <row>
                        <entry>RADIUS</entry>
                        <entry>1.3</entry>
                    </row>
                </tbody>
            </tgroup>
        </informaltable>
        </para>
    </section>
</section>
