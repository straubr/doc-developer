<?xml version='1.0' encoding='UTF-8'?>
<!DOCTYPE section PUBLIC "-//OASIS//DTD DocBook XML V4.4//EN" "http://www.oasis-open.org/docbook/xml/4.4/docbookx.dtd">


<section id="log-module">

    <title>Naplózás modul</title>

    <para>
Létezik egy globális naplózó felület az OTRS-hez, amely a saját naplózó
háttérprogramok létrehozásának lehetőségét biztosítja.
    </para>
    <para>
Egy saját naplózó háttérprogram írása olyan egyszerű, mint újra
megvalósítani a <literal>Kernel::System::Log::Log()</literal> metódust.
    </para>
    <section id="log-module-code">
        <title>Kódpélda: <literal>Kernel::System::Log::CustomFile</literal></title>
        <para>
            Ebben a kis példában írni fogunk egy kicsi fájl naplózó háttérprogramot,
amely hasonlóan működik mint a <literal>Kernel::System::Log::File</literal>,
de egy szöveget fűz minden naplóbejegyzés elé.
        </para>

        <para>
        <programlisting linenumbering="numbered"><![CDATA[
# --
# Kernel/System/Log/CustomFile.pm - file log backend
# Copyright (C) 2001-2017 OTRS AG, http://otrs.com/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::Log::CustomFile;

use strict;
use warnings;

umask "002";

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # get needed objects
    for (qw(ConfigObject EncodeObject)) {
        if ( $Param{$_} ) {
            $Self->{$_} = $Param{$_};
        }
        else {
            die "Got no $_!";
        }
    }

    # get logfile location
    $Self->{LogFile} = '/var/log/CustomFile.log';

    # set custom prefix
    $Self->{CustomPrefix} = 'CustomFileExample';

    # Fixed bug# 2265 - For IIS we need to create a own error log file.
    # Bind stderr to log file, because IIS do print stderr to web page.
    if ( $ENV{SERVER_SOFTWARE} && $ENV{SERVER_SOFTWARE} =~ /^microsoft\-iis/i ) {
        if ( !open STDERR, '>>', $Self->{LogFile} . '.error' ) {
            print STDERR "ERROR: Can't write $Self->{LogFile}.error: $!";
        }
    }

    return $Self;
}

sub Log {
    my ( $Self, %Param ) = @_;

    my $FH;

    # open logfile
    if ( !open $FH, '>>', $Self->{LogFile} ) {

        # print error screen
        print STDERR "\n";
        print STDERR " >> Can't write $Self->{LogFile}: $! <<\n";
        print STDERR "\n";
        return;
    }

    # write log file
    $Self->{EncodeObject}->SetIO($FH);
    print $FH '[' . localtime() . ']';
    if ( lc $Param{Priority} eq 'debug' ) {
        print $FH "[Debug][$Param{Module}][$Param{Line}] $Self->{CustomPrefix} $Param{Message}\n";
    }
    elsif ( lc $Param{Priority} eq 'info' ) {
        print $FH "[Info][$Param{Module}]  $Self->{CustomPrefix} $Param{Message}\n";
    }
    elsif ( lc $Param{Priority} eq 'notice' ) {
        print $FH "[Notice][$Param{Module}] $Self->{CustomPrefix} $Param{Message}\n";
    }
    elsif ( lc $Param{Priority} eq 'error' ) {
        print $FH "[Error][$Param{Module}][$Param{Line}] $Self->{CustomPrefix} $Param{Message}\n";
    }
    else {

        # print error messages to STDERR
        print STDERR
            "[Error][$Param{Module}] $Self->{CustomPrefix} Priority: '$Param{Priority}' not defined! Message: $Param{Message}\n";

        # and of course to logfile
        print $FH
            "[Error][$Param{Module}] $Self->{CustomPrefix} Priority: '$Param{Priority}' not defined! Message: $Param{Message}\n";
    }

    # close file handle
    close $FH;
    return 1;
}

1;
        ]]></programlisting>
        </para>
    </section>
    <section id="log-module-configuration">
        <title>Beállítási példa</title>
        <para>
            Az egyéni naplózómodul bekapcsolásához az adminisztrátor beállíthatja kézzel
a meglévő <literal>LogModule</literal> konfigurációs elemet a
<literal>Kernel::System::Log::CustomFile</literal> osztályhoz. Ennek
automatikus megvalósításához megadhat egy XML beállítófájlt, amely
felülbírálja az alapértelmezett beállítást.
        </para>
        <para>
        <programlisting linenumbering="numbered"><![CDATA[
<ConfigItem Name="LogModule" Required="1" Valid="1">
    <Description Translatable="1">Set Kernel::System::Log::CustomFile as default logging backend.</Description>
    <Group>Framework</Group>
    <SubGroup>Core::Log</SubGroup>
    <Setting>
        <Option Location="Kernel/System/Log/*.pm" SelectedID="Kernel::System::Log::CustomFile"></Option>
    </Setting>
</ConfigItem>
        ]]></programlisting>
        </para>
    </section>
    <section id="log-module-use_cases">
        <title>Használati eset példák</title>
        <para>
Hasznos naplózó háttérprogram lehet egy webszolgáltatásba vagy egy
titkosított fájlba történő naplózás.
        </para>
    </section>
    <section id="log-module-caveats">
        <title>Ellenjavaslatok és figyelmeztetések</title>
        <para>
Ne feledje, hogy a <literal>Kernel::System::Log</literal> a
<literal>Log()</literal> metóduson kívül egyéb metódusokkal is rendelkezik,
amelyeket nem lehet újra megvalósítani, például az osztott
memóriaszakaszokkal történő munkához tartozó kód és a naplóadatok
gyorsítótárazása.
        </para>
    </section>
</section>
