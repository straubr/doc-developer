<?xml version='1.0' encoding='UTF-8'?>
<!DOCTYPE section PUBLIC "-//OASIS//DTD DocBook XML V4.4//EN" "http://www.oasis-open.org/docbook/xml/4.4/docbookx.dtd">

<section id="dynamic-fields-new-field-driver">
    <title>Dynamic Field Driver Example</title>
    <para>
        The driver <emphasis>is</emphasis> the Dynamic Field. It contains several functions that
        are used wide in the OTRS framework. A driver can inherit some functions form base classes,
        for example <literal>TextArea</literal> driver inherits most of the functions from
        <filename>Base.pm</filename> and <filename>BaseText.pm</filename> and it only implements
        the functions that requires different logic or results. Checkbox field driver only inherits
        from <filename>Base.pm</filename> as all other functions are very different from any other
        Base driver.
    </para>
    <note>
        <para>
            Please refer to the Perl On-line Documentation (POD) of the module
            <filename>/Kernel/System/DynmicField/Backend.pm</filename> to have the list of all attributes and possible
            return data for each function.
        </para>
    </note>
    <section id="dynamic-fields-new-field-driver-code">
        <title>Code Example:</title>
        <para>
            In this section the Password Dynamic Field driver is shown and explained. This driver
            inherits some functions from <filename>Base.pm</filename> and <filename>BaseText.pm</filename> and only implements the functions
            that needs different results.
         </para>
         <para>
            <programlisting><![CDATA[
# --
# Kernel/System/DynamicField/Driver/Password.pm - Driver for DynamicField Password backend
# Copyright (C) 2001-2018 OTRS AG, http://otrs.com/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (AGPL). If you
# did not receive this file, see http://www.gnu.org/licenses/agpl.txt.
# --

package Kernel::System::DynamicField::Driver::Password;

use strict;
use warnings;

use Kernel::System::VariableCheck qw(:all);
use Kernel::System::DynamicFieldValue;

use base qw(Kernel::System::DynamicField::Driver::BaseText);

our @ObjectDependencies = (
    'Kernel::Config',
    'Kernel::System::DynamicFieldValue',
    'Kernel::System::Main',
);
            ]]></programlisting>
        </para>
        <para>
            This is the common header that can be found in common OTRS modules. The class/package
            name is declared via the <literal>package</literal> keyword. Notice that
            <literal>BaseText</literal> is used as base class.
        </para>
        <para>
            <programlisting><![CDATA[
sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # set field behaviors
    $Self->{Behaviors} = {
        'IsACLReducible'               => 0,
        'IsNotificationEventCondition' => 1,
        'IsSortable'                   => 0,
        'IsFiltrable'                  => 0,
        'IsStatsCondition'             => 1,
        'IsCustomerInterfaceCapable'   => 1,
    };

    # get the Dynamic Field Backend custom extensions
    my $DynamicFieldDriverExtensions
        = $Kernel::OM->Get('Kernel::Config')->Get('DynamicFields::Extension::Driver::Password');

    EXTENSION:
    for my $ExtensionKey ( sort keys %{$DynamicFieldDriverExtensions} ) {

        # skip invalid extensions
        next EXTENSION if !IsHashRefWithData( $DynamicFieldDriverExtensions->{$ExtensionKey} );

        # create a extension config shortcut
        my $Extension = $DynamicFieldDriverExtensions->{$ExtensionKey};

        # check if extension has a new module
        if ( $Extension->{Module} ) {

            # check if module can be loaded
            if (
                !$Kernel::OM->Get('Kernel::System::Main')->RequireBaseClass( $Extension->{Module} )
                )
            {
                die "Can't load dynamic fields backend module"
                    . " $Extension->{Module}! $@";
            }
        }

        # check if extension contains more behaviors
        if ( IsHashRefWithData( $Extension->{Behaviors} ) ) {

            %{ $Self->{Behaviors} } = (
                %{ $Self->{Behaviors} },
                %{ $Extension->{Behaviors} }
            );
        }
    }

    return $Self;
}
            ]]></programlisting>
        </para>
        <para>
            The constructor <literal>new</literal> creates a new instance of the class. According
            to the coding guidelines objects of other classes that are needed in this module have
            to be created in <literal>new</literal>.
        </para>
        <para>
            It is important to define the behaviors correctly as the field might or might not be
            used in certain screens, functions that depends on behaviors that are not active for
            this particular field might not be needed to be implemented.
        </para>
        <note>
            <para>
                Drivers are created only by the <literal>BackendObject</literal> and not directly from any other
                module.
            </para>
        </note>
    <para>
        <programlisting><![CDATA[
sub EditFieldRender {
    my ( $Self, %Param ) = @_;

    # take config from field config
    my $FieldConfig = $Param{DynamicFieldConfig}->{Config};
    my $FieldName   = 'DynamicField_' . $Param{DynamicFieldConfig}->{Name};
    my $FieldLabel  = $Param{DynamicFieldConfig}->{Label};

    my $Value = '';

    # set the field value or default
    if ( $Param{UseDefaultValue} ) {
        $Value = ( defined $FieldConfig->{DefaultValue} ? $FieldConfig->{DefaultValue} : '' );
    }
    $Value = $Param{Value} if defined $Param{Value};

    # extract the dynamic field value from the web request
    my $FieldValue = $Self->EditFieldValueGet(
        %Param,
    );

    # set values from ParamObject if present
    if ( defined $FieldValue ) {
        $Value = $FieldValue;
    }

    # check and set class if necessary
    my $FieldClass = 'DynamicFieldText W50pc';
    if ( defined $Param{Class} && $Param{Class} ne '' ) {
        $FieldClass .= ' ' . $Param{Class};
    }

    # set field as mandatory
    $FieldClass .= ' Validate_Required' if $Param{Mandatory};

    # set error css class
    $FieldClass .= ' ServerError' if $Param{ServerError};

    my $HTMLString = <<"EOF";
<input type="password" class="$FieldClass" id="$FieldName" name="$FieldName" title="$FieldLabel" value="$Value" />
EOF

    if ( $Param{Mandatory} ) {
        my $DivID = $FieldName . 'Error';

        # for client side validation
        $HTMLString .= <<"EOF";
    <div id="$DivID" class="TooltipErrorMessage">
        <p>
            \$Text{"This field is required."}
        </p>
    </div>
EOF
    }

    if ( $Param{ServerError} ) {

        my $ErrorMessage = $Param{ErrorMessage} || 'This field is required.';
        my $DivID = $FieldName . 'ServerError';

        # for server side validation
        $HTMLString .= <<"EOF";
    <div id="$DivID" class="TooltipErrorMessage">
        <p>
            \$Text{"$ErrorMessage"}
        </p>
    </div>
EOF
    }

    # call EditLabelRender on the common Driver
    my $LabelString = $Self->EditLabelRender(
        %Param,
        DynamicFieldConfig => $Param{DynamicFieldConfig},
        Mandatory          => $Param{Mandatory} || '0',
        FieldName          => $FieldName,
    );

    my $Data = {
        Field => $HTMLString,
        Label => $LabelString,
    };

    return $Data;
}
            ]]></programlisting>
        </para>
        <para>
            This function is the responsible to create the HTML representation of the field and its
            label, and is used in the edit screens like <literal>AgentTicketPhone</literal>, <literal>AgentTicketNote</literal>, etc.
        </para>
        <para>
            <programlisting><![CDATA[
sub DisplayValueRender {
    my ( $Self, %Param ) = @_;

    # set HTMLOutput as default if not specified
    if ( !defined $Param{HTMLOutput} ) {
        $Param{HTMLOutput} = 1;
    }

    my $Value;
    my $Title;

    # check if field is set to show password or not
    if (
        defined $Param{DynamicFieldConfig}->{Config}->{ShowValue}
        && $Param{DynamicFieldConfig}->{Config}->{ShowValue} eq 'Yes'
        )
    {

        # get raw Title and Value strings from field value
        $Value = defined $Param{Value} ? $Param{Value} : '';
        $Title = $Value;
    }
    else {

        # show the mask and not the value
        $Value = $Param{DynamicFieldConfig}->{Config}->{ValueMask} || '';
        $Title = 'The value of this field is hidden.'
    }

    # HTMLOutput transformations
    if ( $Param{HTMLOutput} ) {
        $Value = $Param{LayoutObject}->Ascii2Html(
            Text => $Value,
            Max => $Param{ValueMaxChars} || '',
        );

        $Title = $Param{LayoutObject}->Ascii2Html(
            Text => $Title,
            Max => $Param{TitleMaxChars} || '',
        );
    }
    else {
        if ( $Param{ValueMaxChars} && length($Value) > $Param{ValueMaxChars} ) {
            $Value = substr( $Value, 0, $Param{ValueMaxChars} ) . '...';
        }
        if ( $Param{TitleMaxChars} && length($Title) > $Param{TitleMaxChars} ) {
            $Title = substr( $Title, 0, $Param{TitleMaxChars} ) . '...';
        }
    }

    # create return structure
    my $Data = {
        Value => $Value,
        Title => $Title,
    };

    return $Data;
}
            ]]></programlisting>
        </para>
        <para>
            <code>DisplayValueRender()</code> function returns the field value as a plain
            text as well as its title (both can be translated). For this particular example we are
            checking if the password should be revealed or display a predefined mask by  a
            configuration parameter in the Dynamic Field.
        </para>
        <para>
            <programlisting><![CDATA[
sub ReadableValueRender {
    my ( $Self, %Param ) = @_;

    my $Value;
    my $Title;

    # check if field is set to show password or not
    if (
        defined $Param{DynamicFieldConfig}->{Config}->{ShowValue}
        && $Param{DynamicFieldConfig}->{Config}->{ShowValue} eq 'Yes'
        )
    {

        # get raw Title and Value strings from field value
        $Value = $Param{Value} // '';
        $Title = $Value;
    }
    else {

        # show the mask and not the value
        $Value = $Param{DynamicFieldConfig}->{Config}->{ValueMask} || '';
        $Title = 'The value of this field is hidden.'
    }

    # cut strings if needed
    if ( $Param{ValueMaxChars} && length($Value) > $Param{ValueMaxChars} ) {
        $Value = substr( $Value, 0, $Param{ValueMaxChars} ) . '...';
    }
    if ( $Param{TitleMaxChars} && length($Title) > $Param{TitleMaxChars} ) {
        $Title = substr( $Title, 0, $Param{TitleMaxChars} ) . '...';
    }

    # create return structure
    my $Data = {
        Value => $Value,
        Title => $Title,
    };

    return $Data;
}
            ]]></programlisting>
        </para>
        <para>
            This function is similar to <code>DisplayValueRender()</code> but is used  in
            places where there is no <literal>LayoutObject</literal>.
        </para>
        <para>
            <programlisting><![CDATA[
1;

=back

=head1 TERMS AND CONDITIONS

This software is part of the OTRS project (L<http://otrs.org/>).

This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see L<http://www.gnu.org/licenses/agpl.txt>.

=cut
            ]]></programlisting>
        </para>
        <para>
            The normal OTRS footer for a Perl module file.
        </para>
    </section>

    <section id="dynamic-fields-new-field-driver-other-functions">
        <title>Other Functions:</title>
        <para>
            The following are other functions that are might needed if the new Dynamic Field does
            not inherit from other classes. To see the complete code of this functions please take a
            look directly into the files <filename>Kernel/System/DynamicField/Driver/Base.pm</filename> and
            <filename>Kernel/System/DynamicField/Driver/BaseText.pm</filename>
        </para>
        <para>
            <programlisting><![CDATA[
sub ValueGet { ... }
            ]]></programlisting>
        </para>
        <para>
            This function retrieves the value from the field on a specified Object. In this case we
            are returning the first text value, since the field only stores one text value at time.
        </para>
        <para>
            <programlisting><![CDATA[
sub ValueSet { ... }
            ]]></programlisting>
        </para>
        <para>
            <code>ValueSet()</code> is used to store a Dynamic Field value. In this case
            this field only stores one text type value. Other fields could store more than one
            value on either <literal>ValueText</literal>, <literal>ValueDateTime</literal> or <literal>ValueInt</literal> format.
        </para>
        <para>
            <programlisting><![CDATA[
sub ValueDelete { ... }
            ]]></programlisting>
        </para>
        <para>
            This function is used to delete one field value attached to a particular object ID.
            For example if the instance of an object is to be deleted, then there is no reason to
            have the field value stored in the database for that particular object instance.
        </para>
        <para>
            <programlisting><![CDATA[
sub AllValuesDelete { ... }
            ]]></programlisting>
        </para>
        <para>
            <code>AllValuesDelete()</code> function is used to delete all values from a
            certain Dynamic Field. This function is very useful when a Dynamic Field is going to be
            deleted.
        </para>
        <para>
            <programlisting><![CDATA[
sub ValueValidate { ... }
            ]]></programlisting>
        </para>
        <para>
            The <code>ValueValidate()</code> function is used to check if the value is
            consistent to its type.
        </para>
        <para>
            <programlisting><![CDATA[
sub SearchSQLGet { ... }
            ]]></programlisting>
        </para>
        <para>
            This function is used by <literal>TicketSearch</literal> core module to build the internal query to search
            for a ticket based on this field as a search parameter.
        </para>
        <para>
            <programlisting><![CDATA[
sub SearchSQLOrderFieldGet { ... }
            ]]></programlisting>
        </para>
        <para>
            The <literal>SearchSQLOrderFieldGet</literal> is also a helper for <literal>TicketSearch</literal>
            module. <literal>$Param{TableAlias}</literal> should be kept and <literal>value_text</literal> could be replaced with
            <literal>value_date</literal> or <literal>value_int</literal> depending on the field.
        </para>
        <para>
            <programlisting><![CDATA[
sub EditFieldValueGet { ... }
            ]]></programlisting>
        </para>
        <para>
            <code>EditFieldValueGet()</code> is a function used in the edit screens of OTRS
            and its purpose is to get the value of the field, either from a template like generic
            agent profile or from a web request. This function gets the web request in the
            <literal>$Param{ParamObject}</literal>, that is a copy of the <literal>ParamObject</literal> of the Frontend Module or screen.
        </para>
        <para>
            There are two return formats for this function, the normal: that is just the raw value
            or a structure: that is the pair field name =&gt; field value. For example a Date Dynamic
            Field returns normally the date as string, and if it should return a structure it
            returns a pair for each part of the date in the hash.
        </para>
        <para>
            If the result should be a structure then, normally this is used to store its values
            in a template, like a generic agent profile. For example a date field uses several HTML
            components to build the field, like the "Used" check-box and selects for year, month,
            day etc.
        </para>
        <para>
            <programlisting><![CDATA[
sub EditFieldValueValidate { ... }
            ]]></programlisting>
        </para>
        <para>
            This function should provide at least a method to validate if the field is empty, and
            return an error if the field is empty and mandatory, but it can also do more validations
            for other kind of fields, like if the option selected is valid, or if a date should be
            only in the past etc. It can provide a custom error message also.
        </para>
        <para>
            <programlisting><![CDATA[
sub SearchFieldRender { ... }
            ]]></programlisting>
        </para>
        <para>
            This function is used by ticket search dialog and it is similar to
            <code>EditFieldRander()</code>, but normally on a search screen small changes
            has to be done for all fields. For this example we use a HTML text input instead of a
            password input. In other fields like Dropdown field is displayed as a Multiple select in
            order to let the user search for more than one value at a time.
        </para>
        <para>
            <programlisting><![CDATA[
sub SearchFieldValueGet { ... }
            ]]></programlisting>
        </para>
        <para>
            Very similar to <code>EditFieldValueGet()</code>, but uses a different name
            prefix, adapted for the search dialog screen.
        </para>
        <para>
            <programlisting><![CDATA[
sub SearchFieldParameterBuild { ... }
            ]]></programlisting>
        </para>
        <para>
            <code>SearchFieldParameterBuild()</code> is used also by the ticket search
            dialog to set the correct operator and value to do the search on this field. It also
            returns how the value should be displayed in the used search attributes in the results
            page.
        </para>
        <para>
            <programlisting><![CDATA[
sub StatsFieldParameterBuild { ... }
            ]]></programlisting>
        </para>
        <para>
            This function is used by the stats modules. It includes the field definition in the
            stats format. For fields with fixed values it also includes all this possible values
            and if they can be translated, take a look to the <literal>BaseSelect</literal> driver code for an example
            how to implement those.
        </para>
        <para>
            <programlisting><![CDATA[
sub StatsSearchFieldParameterBuild { ... }
            ]]></programlisting>
        </para>
        <para>
            <code>StatsSearchFieldParameterBuild()</code> is very similar to the
            <code>SearchFieldParameterBuild()</code>. The difference is that the
            <code>SearchFieldParameterBuild()</code> gets the value from the search profile
            and this one gets the value directly from its parameters.
        </para>
        <para>
            This function is used by statistics module.
        </para>
        <para>
            <programlisting><![CDATA[
sub TemplateValueTypeGet { ... }
            ]]></programlisting>
        </para>
        <para>
            The <code>TemplateValueTypeGet()</code> function is used to know how the
            Dynamic Field values stored on a profile should be retrieved, as a SCALAR or as an
            ARRAY, and it also defines the correct name of the field in the profile.
        </para>
        <para>
            <programlisting><![CDATA[
sub RandomValueSet { ... }
            ]]></programlisting>
        </para>
        <para>
            This function is used by <filename>otrs.FillDB.pl</filename> script to populate the database with some test
            and random data. The value inserted by this function is not really relevant. The only
            restriction is that the value must be compatible with the field value type.
        </para>
        <para>
            <programlisting><![CDATA[
sub ObjectMatch { ... }
            ]]></programlisting>
        </para>
        <para>
            Used by the notification modules. This function returns 1 if the field is present in the
            <literal>$Param{ObjectAttributes}</literal> parameter and if it matches the given value.
        </para>
    </section>
</section>
